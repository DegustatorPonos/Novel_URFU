label act2_Gal_meet2:
    "Вы заходите в офис и слышите крики со стороны кабинета Галины."
    "Подойдя ближе к кабинету вам в нос ударяет сильный запах мочи." 
    scene GalBG
    "Войдя в кабинет вы замечаете несколько зловонных луж на полу и то, как Галина Петровна, держа в руках трех котят, спорит с Геннадием и Евгением Викторовичем"
    show EVSad:
        xalign 0.5
    show GenScream:
        xalign -1
    show GalSad:
        xalign 1.0
        xzoom -1.0
    Gennadiy "...Короче, Евгений Викторович, сами ее образумьте, я так больше не могу!"
    "Геннадий в спешке, прикрыв нос, покидает кабинет."
    hide GenScream 
    with fade
    show MCIdle:
        xalign -1
    menu:
        "(Уйти вслед за Геннадием)":
            $GalAtt -= 2
            return
        "Добрый день, что за шум?":
            if EVatt > 0:
                EV "А ты не видишь? Мы тут зоопарк открыли!"
            else:
                EV "Тебе какое дело? Врываешься посреди разговора!"
            Galina "Закатывают тут скандалы!{w} Говорю же, сейчас все уберу и освежителем пройдусь, ни следа не останется!"
            menu:
                "Вступиться за Галину":
                    $GalAtt += 1
                    mainChar "И в самом деле, Евгений Викторович, тут прибраться и воздух очистить пустяки, а вы раздуваете скандалы!"
                    EV "Нет ну как так то?{w} Ладно прибраться, но мы на работе в конце то концов, и вы сюда своих зверей таскаете, наверняка еще с какой заразой на себе!"
                "Вступиться за Евгения Викторовича":
                    $GalAtt -= 1
                    mainChar "Нет ну правда, Галина Петровна!{w} Что за зверинец вы тут устраиваете?{w} Тащите их домой себе и убирайте за ними там!"
                    EV "И в самом деле, это не нормально!"
                "Промолчать":
                    EV "Нет ну как так то?{w} Ладно прибраться, но мы на работе в конце то концов, и вы сюда своих зверей таскаете, наверняка еще с какой заразой на себе!"
    Galina "Но ведь я их сегодня утром нашла, у помойки когда проходила, не могла уже домой вернуться!"
    menu:
        "Вступиться за Галину":
            $GalAtt += 1 
            mainChar "И в самом деле, это ведь в первый и наверняка в последний раз произошло, не нужно из этого раздувать проблему."
            EV "Да, но это же... {w}к черту, я очень надеюсь что этого больше не повторится!"
            "Раздраженный Евгений Викторович покидает кабинет." 
            hide EVSad
            with dissolve
            hide GalSad
            show GalSmile:
                xalign 1.0
                xzoom -1.0
            Galina "Фух, успокоились наконец.{w} Спасибо что остудил его."
            mainChar "Ой, да ладно."
            "Вы выходите из кабинета Галины."
            hide MCIdle
            hide GalSmile
            scene bg_bbg
            return
        "Вступиться за Евгения":
            $GalAtt -= 1
            mainChar "Нет ну вы в своем уме? {w}Оставили бы их там, если выбросили значит и смысла от них не было."
            Galina "Что!? Да как ты можешь такое говорить?"
            call gal2_complainings
            "Евгений Викторович покидает кабинет, а после за ним выходит Галина Петровна, изо всех сил стараясь спрятать слезы."
        "Промолчать":
            call gal2_complainings
            "Евгений Викторович покидает кабинет, а после за ним выходит Галина Петровна, изо всех сил стараясь изобразить максимально оскорбленное лицо."
    hide GalSad with dissolve
    hide EVSad with dissolve
    "Вы выходите из кабинета Галины следом за ними."
    hide MCIdle
    scene bg_bbg
    return

label gal2_complainings:
    EV "Так, хватит." with vpunch
    EV "Галина Петровна, даю вам на сегодня выходной, забирайте своих животных и едьте занимайтесь ими дома, понятно?"
    Galina "Но Евгений Викторович..."
    EV "Я все сказал, увидимся завтра."
    return
    